DELIMITERS = "<.>" "<!>" "<?>";

LIST noun = n;
LIST inflected = vfle;
LIST infinitive = vinf infin;
LIST preposition = p prep;
LIST pronoun = r;
LIST interrogative = part-int;
LIST adjective = a adj;
LIST adverb = b;
LIST conjunction = c;
LIST prequal = "ofnadwy" "andros" "tipyn";
LIST concessive = "oh" "well";

LIST amvinf = (vinf am);
LIST smvinf = (vinf sm);

LIST amnoun = (n am);
LIST nmnoun = (n nm);
LIST smnoun = (n sm);

LIST possessive = ("ei" :her:) ("ei" :his:);

LIST modal = "licio" "gobeithio" "gallu";

LIST countable = "munud" "ceiniog";

#SET amvnoun = amvinf or amnoun;
# note use of "or" (|) above; "and" (+) does not work because it yields all possible combinations of the elements in the two sets; since the n and vinf tags will never occur together, the set will never be valid; the following has the same effect:
LIST amvnoun = (vinf am) or (n am);


SECTION

##### Rules for Welsh #####

# Particles

select interrogative if (@1 ("a")) (@-1 ("<?>"));
select ("na" b) if (@1 ("na"));
select ("na" b) if (-1 concessive);
select ("na" b) if (1 inflected);

select ("yn" stat) if ((1 infinitive) or (1 adjective) or (1 adverb) or (1 noun + (sm)));

select ("y" t) if (not 1 inflected);

select ("ddim" b) if (-1 pronoun) (-2 inflected)
select ("ddim" b) if (-1 inflected);
select ("dim" b) if (1 noun);

select ("mor" :so:) if (not 2 ("â" c));
select ("mor" a) if (2 ("â" c));

select ("â" c) if (-2 ("mor" a));
select ("â" p) if (not -2 ("mor" a));

select ("ynte" b) if (1 ("<?>"));

select ("do" b) if (1 ("<.>"));
select ("do" b) if (1 ("<?>"));

remove ("ni" part-neg) if (not 1 inflected);

select ("beth" i) if (1 inflected);


# Conjunctions

select ("a" c) if (-1 (name)) (1 (name));
select ("a" c) if (not 1 (@1));


# Interrogatives

select ("beth" i) if (1 ("am" p));


# Prepositions

select ("yn" p) if (1 (t));
select ("yn" p) if (1 ("ystod"));
select ("yn" p) if (1 (name));

#select ("i" p) if (1 (sm));
select ("i" p) if (not -1 inflected);

#select ("o" :of:) if (-1 prequal) (1 adjective);
select ("o" :of:) if (-1 prequal or ("maint"));
select ("o" :of:) if (-2 ("ychydig"));
select ("o" :from:) if (1 ("nos" :night:));
select ("o" :of:) if (1 ("blaen" sm));
select ("o" :of:) if (-1 ("chwech" a));  # needs to be generalised to all numbers by retagging them as "num", not "a"
select ("o" :of:) if (1 infinitive + (sm));

select ("allan" b) if (-1 ("sefyll") or ("mynd"));


# Pronouns

select ("i" r) if (-1 ("bod" 1s present spoken));
select pronoun if (-1 inflected);
select pronoun if (-1 infinitive);

select pronoun if ((-2 possessive) or (-3 possessive));
select pronoun if (-1 preposition);


# Nouns

remove nmnoun if (not -1 ("yn" "fy"));
select noun if (-1 ("y" t));

select ("ystod" :range:) if (-1 ("yn" p));

select ("ysgol" :school:) if (-1 ("y" t)) (-2 ("yn" p));
select ("ysgol" :school:) if (1 ("uwchradd" "Cymraeg"));

select ("gwaith" :work:) if (-1 ("o" :of:));
select ("gwaith" :work:) if (-1 ("codi" :lift:));

select ("diolch" n) if (-1 ("dweud" vinf));

select ("man" :place:) if (1 ("acw" b) or ("hyn" r));

select ("Mawrth" :March:) if (-1 ("mis" n));
select ("Mawrth" :Tuesday:) if (-1 ("nos" n));
select ("Mercher" :Wednesday:) if (-1 ("nos" n));

select ("ymarfer" n) if (-1 ("angen" n));

select ("Cymraeg" n) if (-1 ("siarad"));


# Adjectives

select ("ei" a :her:) if (1 amvnoun);
select ("ei" a :his:) if (1 smnoun);
select ("ei" a :his:) if (1 (i)) (2 smnoun);
select ("ei" a :her:) if (1 noun) (2 ("hi" r));
select possessive if (1 noun);

select ("iawn" :OK:) if (-1 ("yn" stat));
select ("iawn" :OK:) if (-1 concessive);
select ("iawn" :OK:) if (@1 ("iawn")) (@-1 ("<.>"));
select ("iawn" :very:) if (-1 adjective);

select ("deg" :ten \(10\):) if (1 countable);


# Verbs

select ("bod" future) if (not 1 ("yn" stat));
select ("bod" 3p present) if (1 pronoun);
select ("bod" vinf) if (-1 ("heb" p));
select ("bod" vinf) if (-1 ("heb" p));
select ("bod" vfle present) if (0 ("<oes>")) (1 ("<.>"));


select infinitive if (-1 ("yn" stat));
select (vinf sm) if (-1 ("i" r));  # dw i meddwl
select infinitive if (-1 possessive);
select infinitive if (-1 modal);
select infinitive if (-1 preposition);
select infinitive if (-1 ("bod" vfle));
select infinitive if (1 ("<.>"));
#select inflected if (1 ("<.>"));
select infinitive if (-1 pronoun);
select infinitive if (-1 ("<sy>"));
select inflected if (1 pronoun);
select (1p) if (1 ("ni" r));


#Adverbs

select ("allan" b) if (-1 ("pell"));


# Global rules

# 1- homonym selection - has the effect of "deleting" items from the dictionary
select ("gofyn" :ask:);
select ("cyrraedd" :arrive:);
select ("rhyw" :some:);
select ("cyfeiriad" :direction:);
select ("mynd" vfle 1p present);
select ("darn" :pieces:);
select ("aros" :wait:);
remove (vfle subjunctive);
remove ("fod" c :that:);
remove ("cael" :get:);
remove ("gwadu" vfle sm 2s present);
remove ("meddu" vfle 3s imperfect);
remove ("tebygol" a sm :probable:);

# 2 - delete archaic forms
remove (archaic);


##### Rules for Spanish #####


# Articles

select ("el"  det.def) if (1 (n f) or (adj f) or (ord f));
select ("el"  det.def) if (1 (n m) or (adj m) or (ord m));
select (det.def) if (-1 ("de" prep :of:));

select ("uno" [es] det.indef) if (1 (n f) or (adj f) or (ord f));
select ("uno" [es] det.indef) if (1 (n m) or (adj m) or (ord m));


# Conjunctions

select ("así" adv) if (1 (vfle));
select ("que" :which:) if (not -1 (adjective));


# Prepositions

select ("para" [es] prep :for:) if (1 (prn.obj) or  (v infin) or ("que" conj :which:) or (det.def));
select ("entre" [es] prep :between:) if (not 1 ("en"));
select ("como" [es] prep :like:) if (1 (prn.subj) or ("que" conj :which:));


# Nouns

select (n m sg) if (-1 ([es] det.def m sg) or ([es] det.indef m sg));
remove (n m sg) if (not -1 ([es] det.def m sg) or ([es] det.indef m sg));
select (n m pl) if (-1 ([es] det.def m pl) or ([es] det.indef m pl));
select (n f sg) if (-1 ([es] det.def f sg) or ([es] det.indef f sg));
select (n f pl) if (-1 ([es] det.def f pl) or (det.indef f pl));
select ("gusto" n) if (-1 ("mucho" adj));
select (n) if (1 ("del"));
select ("casa" n f sg :house:) if (-1 (prep));


# Demonstratives

select ("este" adj.dem m sg) if (1 (n m sg));


# Pronouns

select (prn.refl) if (1 (v 3s) or (v 13s) or (v 3p));
select ("él" [es] prn.obj) if (1 (v));
select ("otro" prn) if (not 1 (n));
select ("uno" [es] prn) if (1 ("de"));
select ("todo" prn) if (1 (v));
select ("temprano" adv) if (-1 ("muy"));
select (prn.obj) if (1 ("que" conj :which:));

# Adjectives

select ([es] adj m sg) if (-1 ([es] n m sg));
select ("mucho" adj) if (1 (n m sg));
select ("tarde" n) if (-1 (det.def));
select ("mejor" adj) if (1 (n));
select ("todo" [es] adj) if (1 (det.def));

#  Verbs

select (pastpart) if (-1 ("haber"));
select ("ser" [es] v 13s imperf :be:) if (not 1 (v));
select ("ser" [es] v 3s past :be:) if (not 1 (preposition));
select (v 3s pres) if (-1 ("donde" rel :where:));
select (v infin) if (-1 ("para" [es] prep :for:));


# Global rules

# 1- remove less common tenses - has the effect of "deleting" items from the dictionary
select (v 3s pres);
